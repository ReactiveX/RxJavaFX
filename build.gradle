buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.5.2"
        classpath "org.javamodularity:moduleplugin:1.5.0"
    }
}

apply plugin: "java"
apply plugin: "org.javamodularity.moduleplugin"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "com.jfrog.artifactory"


group = GROUP
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


boolean isReleaseBuild = gradle.startParameter.taskNames.contains('bintrayUpload')
version = isReleaseBuild ? VERSION_NAME : "$VERSION_NAME-SNAPSHOT"
logger.info("RxJavaFX using version=$project.version")


def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.reactivex.rxjava3:rxjava:3.0.1'
    compile "org.openjfx:javafx-base:11:${platform}"
    compile "org.openjfx:javafx-graphics:11:${platform}"
    compile "org.openjfx:javafx-controls:11:${platform}"
    testCompile 'junit:junit-dep:4.11'
    testCompile 'org.mockito:mockito-core:1.8.5'
}

test {
    moduleOptions {
        runOnClasspath = true
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true
//        links "https://docs.oracle.com/javase/8/docs/api/" // causes build to fail on openjdk-11.0.2+9
    }
}

task packageJavadoc(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact packageJavadoc

            groupId GROUP
            version VERSION_NAME
            artifactId ARTIFACT_ID

            pom {
                name = POM_NAME
                description = POM_DESCRIPTION
                url = POM_URL

                licenses {
                    license {
                        name = POM_LICENCE_NAME
                        url = POM_SCM_URL
                        distribution = POM_LICENCE_DIST
                    }
                }
                developers {
                    developer {
                        id = "thomasnield"
                        name = "Thomas Nield"
                        email = "thomasnield@live.com"
                        organization = "ReactiveX"
                        organizationUrl = "http://reactivex.io/"
                    }
                }
                scm {
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                    url = "scm:git:git@github.com:ReactiveX/RxJavaFX.git"
                }
                issueManagement {
                    system = "github"
                    url = "https://github.com/ReactiveX/RxJavaFX/issues"
                }
            }
        }
    }
}
bintray {
    publications = ['mavenJava']

    publish = true
    pkg {
        repo = 'RxJava'
        name = 'RxJavaFX'
        userOrg = 'reactivex'
        licenses = ['Apache-2.0']
        labels = ['rxjava', 'reactivex']
        websiteUrl = 'https://github.com/ReactiveX/RxJavaFX/'
        issueTrackerUrl = 'https://github.com/ReactiveX/RxJavaFX/issues'
        vcsUrl = "https://github.com/ReactiveX/RxJavaFX.git"
        
        version {
            name = VERSION_NAME
            vcsTag = VERSION_NAME
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = true
                user = rootProject.sonatypeUsername
                password = rootProject.sonatypePassword

                name = POM_NAME
                description = POM_DESCRIPTION

                close = "1"
            }
        }

    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

